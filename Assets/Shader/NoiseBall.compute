#pragma kernel VertexBufferUpdate
#pragma kernel IndexBufferUpdate

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Macros.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Random.hlsl"
#include "Assets/Shader/SimplexNoise3D.hlsl"

uint TriangleCount;
float TriangleExtent;
float NoiseFrequency;
float NoiseAmplitude;
float3 NoiseOffset;

RWByteAddressBuffer Vertices;
RWByteAddressBuffer Indices;

// Uniformly distributed random points
// 厳密なランダム関数な気がする。
float3 RandomPoint(uint seed)
{
    seed *= 2;
    // Unityの組み込み関数
    float u = Hash(seed) * 3.141592 * 2;
    float z = Hash(seed + 1) * 2 - 1;
    float w = sqrt(1 - z * z);
    return float3(cos(u) * w, sin(u) * w, z);
}

// Write a vertex data to ByteAddressBuffer
void WriteVertex(uint vidx, float3 p, float3 n)
{
    // 頂点 float3 + 法線float3 * 4bytes 
    uint addr_p = vidx * 6 * 4;
    // float3 * 4 bytes
    uint addr_n = addr_p + 3 * 4;
    Vertices.Store3(addr_p, asuint(p));
    Vertices.Store3(addr_n, asuint(n));
}

// Kernel 0: Vertex buffer update task
[numthreads(64, 1, 1)]
void VertexBufferUpdate(uint id : SV_DispatchThreadID)
{
    if (id >= TriangleCount) return;

    // 
    uint vidx = id * 3;

    // Three random vertices
    // 三個のランダムなポイントを作成する 
    float3 v1 = RandomPoint(vidx + 0);
    float3 v2 = RandomPoint(vidx + 1);
    float3 v3 = RandomPoint(vidx + 2);

    // Triangle size normalization

    // v1を基準にしてv2の相対ベクトルを作成。TriangleExtentで指定した分法線をスケールする　ｖ1を足して正規化することで新しい法線を作成。
    v2 = normalize(v1 + normalize(v2 - v1) * TriangleExtent);
    v3 = normalize(v1 + normalize(v3 - v1) * TriangleExtent);

    // Noise displacement modifier　＝＞ノイズ関数で三角形の頂点をそれぞれずらす。
    float l1 = SimplexNoise(v1 * NoiseFrequency + NoiseOffset);
    float l2 = SimplexNoise(v2 * NoiseFrequency + NoiseOffset);
    float l3 = SimplexNoise(v3 * NoiseFrequency + NoiseOffset);

    // 変異の値を絶対値にする
    v1 *= 1 + abs(l1 * l1 * l1) * NoiseAmplitude;
    v2 *= 1 + abs(l2 * l2 * l2) * NoiseAmplitude;
    v3 *= 1 + abs(l3 * l3 * l3) * NoiseAmplitude;

    // Normal vector
    // ベクトルから法線を算出。
    float3 n = normalize(cross(v2 - v1, v3 - v1));

    // Write to the vertex buffer.
    WriteVertex(vidx + 0, v1, n);
    WriteVertex(vidx + 1, v2, n);
    WriteVertex(vidx + 2, v3, n);
}

// Kernel 1: Index buffer update task
[numthreads(64, 1, 1)]
void IndexBufferUpdate(uint id : SV_DispatchThreadID)
{
    if (id >= TriangleCount) return;

    uint iidx = id * 3;
// Store3を設定する * 4 しているのは４バイトを乗算してメモリアドレスを指定。第二引数に実際のIndexを指定
    Indices.Store3(iidx * 4, uint3(iidx, iidx + 1, iidx + 2));
}
